// ---------------------------------------------------------------------
// performiDisplaceLocatorChain.mel
//
// Creates an option box for creating a dropoff locator chain for the
// iDisplace deformer.
//
// ---------------------------------------------------------------------

proc setOptionVars( int $forceFactorySettings )
{
    if ($forceFactorySettings || !`optionVar -exists iDisplaceLocatorChainCount`)
        optionVar -iv iDisplaceLocatorChainCount 5;
}


global proc iDisplaceLocatorChainSetup( string $parent, int $forceFactorySettings )
{
    setOptionVars($forceFactorySettings);
    setParent $parent;

    intSliderGrp -edit
                 -value `optionVar -q iDisplaceLocatorChainCount`
                 iDisplaceLocatorChainCount;
}


global proc iDisplaceLocatorChainCallback( string $parent, int $doIt )
{
    setParent $parent;

    optionVar -intValue iDisplaceLocatorChainCount
              (`intSliderGrp -q -v iDisplaceLocatorChainCount`);

    if ($doIt)
    {
        performiDisplaceLocatorChain 0;
        string $cmd = "performiDisplaceLocatorChain 0";
        addToRecentCommandQueue $cmd "CreateDropoffLocatorChain";
    }
}


proc iDisplaceLocatorChainOptions()
{
    string $commandName = "iDisplaceLocatorChain";
    string $callback = ($commandName + "Callback");
    string $setup = ($commandName + "Setup");

    setOptionVars 0;

    // get the option box
    string $layout = getOptionBox();
    setParent $layout;

    // activate the option box template
    setUITemplate -pushTemplate DefaultTemplate;
    waitCursor -state 1;

    string $parent = `columnLayout -adjustableColumn 1`;

    frameLayout -label "Settings" -collapse 0 -collapsable 0;
        columnLayout;
            intSliderGrp -label "Locator Count" -min 2 -max 10 iDisplaceLocatorChainCount;
        setParent $parent;

    waitCursor -state 0;

    // deactivate the template
    setUITemplate -popTemplate;

    // customize the buttons
    string $applyBtn = getOptionBoxApplyBtn();
    button -edit
           -label "Create"
           -command ($callback + " " + $parent + " " + 1)
           $applyBtn;

    string $saveBtn = getOptionBoxSaveBtn();
    button -edit
           -command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
           $saveBtn;

    string $resetBtn = getOptionBoxResetBtn();
    button -edit
           -command ($setup + " " + $parent + " " + 1)
           $resetBtn;

    // set the window title
    setOptionBoxTitle("Create Dropoff Locator Chain");

    // edit the help text
    setOptionBoxHelpTag("iDisplaceLocatorChain");

    // set the current values
    eval ($setup + " " + $parent + " " + 0);

    // show the option box
    showOptionBox();
}


proc string assembleCmd()
{
    setOptionVars 0;
    $cmd = "iDisplaceConnectDropoff chain " + `optionVar -q iDisplaceLocatorChainCount`;
    return $cmd;
}


global proc string performiDisplaceLocatorChain( int $option )
{
    string $cmd = "";
    string $sel[];
    switch ($option)
    {
        case 0:
            $cmd = `assembleCmd`;
            eval $cmd;
            break;
        case 1:
            iDisplaceLocatorChainOptions;
            break;
    }
    return $cmd;
}

// ---------------------------------------------------------------------
// MIT License
//
// Copyright (c) 2021 Ingo Clemens, brave rabbit
// iDeform, iCollide, iDisplace, iSkinDeform are under the terms of the
// MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining
// a copy of this software and associated documentation files (the
// "Software"), to deal in the Software without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to
// permit persons to whom the Software is furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Author: Ingo Clemens    www.braverabbit.com
// ---------------------------------------------------------------------
